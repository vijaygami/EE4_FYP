* PSpice Model Editor - Version 16.6.0
*$
* PSpice Model Editor - Version 16.6.0
.SUBCKT VTEAM plus minus 
+PARAMS: initial_x = 0.75
+x_off = 1
+x_on = 0
+v_on = -1
+v_off = 1
+k_on = -0.036363636
+k_off = 0.036363636
+alpha_on = 0.1
+alpha_off = 0.1
+r_off = 50000
+r_on = 500
* Manually calculate lamda = ln(r_off/r_on) *
+lamda = 4.60517
+omega = 25e-3
+a_on = -1
+a_off = 2
****** Memristance dependance on state variable ********
*Linear*
.func mem_rel(x)={r_on+((r_off-r_on)*(x-x_on))/(x_off-x_on)}
*Exponential*
*.func mem_rel(x)={r_on*exp((lamda*(x-x_on))/(x_off-x_on))}
********* Functions **********
.func expL(x)= {if((x<=10), exp(x), exp(10))}
.func gfon(x)= {if( (0 > x),0.1*x, 0)}
.func gfoff(x)= {if((0 < x),0.1*x, 0)}
.func winon(x)={ expL(-expL(-((x-a_on)/omega ))) }
.func winoff(x)={ expL(-expL((x-a_off)/omega )) }
.func poly_iv(x)={x}
********* State Equation ********
Cstate state 0 300mF IC={5}
Gon 0 state VALUE={(gfon(V(plus, minus)))}
Goff 0 state VALUE={(gfoff(V(plus, minus)))}
Vxon p1 0 {0}
Vxoff p2 0 {10}
Dxonlim p1 state Dbreak
Dxofflim state p2 Dbreak
****** Memristor ******
Rshunt plus minus 10000000
Gmemrist plus minus VALUE={(poly_iv(V(plus, minus)))/mem_rel(V(state,0))}
.ENDS
*
*$
