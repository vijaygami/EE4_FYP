* PSpice Model Editor - Version 16.6.0
*$
.SUBCKT TEAM plus minus 
+PARAMS: initial_x = 0.5
+x_off = 1
+x_on = 0
+i_on = -40e-3
+i_off = 40e-3
+k_on = -0.1
+k_off = 0.1
+alpha_on = 0.1
+alpha_off = 0.1
+r_off = 20000
+r_on = 10000
+omega = 25e-3
+a_on = 0.05
+a_off = 0.95
****** Memristance dependnace on state variable ********
*Linear*
.func mem_rel(x)={10000+((25000-10000)*(x-x_on))/(x_off-x_on)}
********* Functions **********
.func expL(x)= {if((x<=10), exp(x), exp(10))}
.func gfon(x)= {if( (i_on > x), (MIN((1*(k_on)* (PWR((( x/i_on)-1), alpha_on ))), 0.99e10)), 0)}
.func gfoff(x)= {if((i_off < x),(MIN((1*(k_off)*(PWR(((x/i_off)-1), alpha_off))), 0.99e10)), 0)}
*.func winon(x)={if((x>x_on),1,0)}
*.func winoff(x)={if((x<x_off),1,0)}
.func winon(x)={ expL(-expL( -(x-a_on)/omega )) }
.func winoff(x)={ expL(-expL(  (x-a_off)/omega )) }
*State Equation*
Cstate state 0 33mF IC={initial_x}
Gon 0 state VALUE={(gfon(I(Visense)))*winon(V(state,0))}
Goff 0 state VALUE={(gfoff(I(Visense)))*winoff(V(state,0))}
Vxoff p2 0 {x_off}
Vxon p1 0 {x_on}
Dxonlim p1 state Dbreak
Dxofflim state p2 Dbreak
****** Memristor ******
Visense n0 minus {0}
Rseries n0 n1 100
Ememrist plus n1 VALUE={I(Visense)*mem_rel(V(state,0))}
.ENDS
*$
