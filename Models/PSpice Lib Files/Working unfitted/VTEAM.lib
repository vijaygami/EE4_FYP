* PSpice Model Editor - Version 16.6.0
*$
.SUBCKT VTEAM plus minus 
+PARAMS: initial_x = 0.5
+x_off = 1
+x_on = 0
+v_on = -1
+v_off = 1
+k_on = -0.1
+k_off = 0.1
+alpha_on = 2
+alpha_off = 2
+r_off = 10000
+r_on = 100
+lamda = 4.60517
+omega = 100e-3
+a_on = 0.2
+a_off = 0.8
****** Memristance dependnace on state variable ********
*Linear*
.func mem_rel(x)={r_on+((r_off-r_on)*(x-x_on))/(x_off-x_on)}
*Exponential*
*.func mem_rel(x)={r_on*exp((lamda*(x-x_on))/(x_off-x_on))}
********* Functions **********
.func expL(x)= {if((x<=10), exp(x), exp(10))}
.func gfon(x)= {if( (v_on > x), (MIN((1*(k_on)* (PWR((( x/v_on)-1), 3 ))), 0.99e10)), 0)}
.func gfoff(x)= {if((v_off < x),(MIN((1*(k_off)*(PWR(((x/v_off)-1), 3))), 0.99e10)), 0)}
.func winon(x)={ expL(-expL( -(x-a_on)/omega )) }
.func winoff(x)={ expL(-expL(  (x-a_off)/omega )) }
********* State Equation ********
Cstate state 0 3nF IC={initial_x}
Gon 0 state VALUE={(gfon(V(plus, minus)))*winon(V(state,0))}
Goff 0 state VALUE={(gfoff(V(plus, minus)))*winoff(V(state,0))}
Vxon p1 0 {x_on}
Vxoff p2 0 {x_off}
Dxonlim p1 state Dbreak
Dxofflim state p2 Dbreak
****** Memristor ******
Rshunt plus minus 1000000
Gmemrist plus minus VALUE={V(plus, minus)/mem_rel(V(state,0))}
.ENDS
*$
