.SUBCKT TEAM plus minus 
+PARAMS: initial_x=0.5
+x_off=1
+x_on=0
+i_on=-50e-6
+i_off=50e-6
+k_on=-3
+k_off=3
+a_on=5
+a_off=5
+R_off=100000
+R_on=1000
+lamda_y=4.60517


.func Gfon(x)= {if((i_on>x), (k_on)*PWR(((x/i_on)-1), a_on), 0)}
.func Gfoff(x)= {if((i_off<x), (k_off)*PWR(((x/i_off)-1), a_off), 0)}
.func Winon(x)={if((x>x_on),1,1e-10)}
.func Winoff(x)={if((x<x_off),1,1e-10)}


*State Equation*
Cx state 0 1F IC={initial_x}
Gon 0 state VALUE={(Gfon(I(Vis)))*Winon(V(state,0))}
Goff 0 state VALUE={(Gfoff(I(Vis)))*Winoff(V(state,0))}
Vxon p1 0 {x_on}
Vxoff p2 0 {x_off}
Dxon p1 state Dbreak
Dxoff state p2 Dbreak


******IV relationships********
*Linear*
.func iv_rel(x)={R_on+((R_off-R_on)*(x-x_on))/(x_on-x_off)}

*Exponential*
*.func iv_rel(x)={R_on*exp((lamda_y*(x-x_on))/(x_off-x_on))}
******************************


Vis n0 minus {0}
Rs n0 n1 0.01
Em plus n1 VALUE={I(Vis)*iv_rel(V(state,0))}

.ENDS